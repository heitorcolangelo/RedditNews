buildscript {
    ext.kotlin = '1.2.30'
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {

    afterEvaluate {

        if (!android) return

        android {
            compileSdkVersion 27
            buildToolsVersion "27.0.3"

            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 27

                versionCode Integer.parseInt(VERSION_CODE)
                versionName VERSION_NAME
            }

            lintOptions {
                abortOnError false
                warning 'InvalidPackage'
            }

            aaptOptions {
                cruncherEnabled = false
            }

            packagingOptions {
                exclude 'META-INF/LICENSE'
            }

            testOptions {
                unitTests.includeAndroidResources = true
                animationsDisabled true
            }

            packagingOptions {
                exclude 'META-INF/LICENSE'
            }

            sourceSets {
                main.java.srcDirs += 'src/main/kotlin'
                test.java.srcDirs += 'src/test/kotlin'
                androidTest.java.srcDirs += 'src/androidTest/kotlin'
                androidTest.resources.srcDirs += 'src/androidTest/res'
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

        }
    }
}

ext {
    supportLibrary = '27.1.0'
    constraintLayout = '1.0.2'
    gson = '2.8.0'
    retrofit = '2.3.0'
    okHttp = '3.8.0'
    timber = '4.6.0'
    rxAndroid = '2.0.1'
    rxJava = '2.1.0'
    picasso = '2.5.2'
    threeten = '1.0.5'

    requestMatcher = '2.2.0'
    hamcrest = '1.3'
    kappuccino = '1.1.0'
    mockito = '2.8.9'
    espresso = '3.0.1'
    rules = '1.0.1'
    runner = '1.0.1'
    jUnit = '4.12'
    mirror = '1.6.1'
    dexopener = '0.12.1'
    testButler = '1.3.1'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
